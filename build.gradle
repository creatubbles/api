buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

plugins {
	id 'java'
	id 'maven'
	id 'eclipse'
	id 'idea'
    id 'net.franz-becker.gradle-lombok'  version '1.5'
}

archivesBaseName = "creatubbles-wsapi"
group = 'com.creatubbles.api'
description = "WS API for Creatubbles"
sourceCompatibility = 1.6
targetCompatibility = 1.6

import org.ajoberstar.grgit.Grgit

if (System.getenv('BUILD_NUMBER') != null) {
    version += "-${System.getenv('BUILD_NUMBER')}"
} else if (new File(projectDir, '.git').exists()) {
    def repo = Grgit.open(project.file('.'))
    version += "-${repo.log().find().abbreviatedId}"
} else {
    version += "-CUSTOM"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.22.1'
}

lombok {
    version = "1.16.8"
    sha256 = "fe32b29b7b33eb2b19866cbc5345c59c9984a6036cc396856a04c0d09f99ea68"
}

import net.franz_becker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask) {
    args("src/main/java", "-d", "build/sources/delomboked/java")
}

tasks.eclipse.dependsOn installLombok

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

build.dependsOn sourceJar, javadoc

artifacts {
    archives jar
    archives sourceJar
}

uploadArchives {
    mavendir = System.getenv('MAVENDIR') ?: project.getProperty('mavendir')
    onlyIf {
        return mavendir
    }
    repositories {
        mavenDeployer {
            repository(url: 'file://' + mavendir)
        }
    }
}
